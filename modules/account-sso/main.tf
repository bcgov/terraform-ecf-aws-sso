data "keycloak_realm" "kc-lz-sso-realm" {
  realm = var.kc_realm
}

data "http" "saml_idp_metadata" {
  url = "${var.kc_base_url}/realms/${var.kc_realm}/protocol/saml/descriptor"
}

resource "aws_iam_saml_provider" "default" {
  name                   = var.aws_saml_idp_name
  saml_metadata_document = data.http.saml_idp_metadata.response_body
}

data "aws_caller_identity" "aws_context" {}

resource "aws_iam_role" "role" {
  for_each = var.account_roles

  name                 = each.key
  max_session_duration = 21600
  permissions_boundary = aws_iam_policy.bcgov_perm_boundary.arn
  managed_policy_arns  = each.value
  assume_role_policy   = <<EOF
{
  "Version": "2012-10-17",
  "Statement": [
    {
      "Effect": "Allow",
      "Principal": {
        "Federated": "arn:aws:iam::${data.aws_caller_identity.aws_context.account_id}:saml-provider/${var.aws_saml_idp_name}"
      },
      "Action": "sts:AssumeRoleWithSAML",
      "Condition": {
        "StringEquals": {
          "SAML:aud": ${jsonencode(var.trusted_login_sources)}
        }
      }
    }
  ]
}
EOF

  depends_on = [
    aws_iam_policy.bcgov_cost_explorer_and_budgets,
  ]
}

resource "aws_iam_policy" "bcgov_perm_boundary" {
  name        = "BCGOV_Permission_Boundary"
  description = "Policy to restrict actions on BCGov Resources"
  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Action   = "*"
        Effect   = "Allow"
        Resource = "*"
        Sid      = "AllowAdminAccess"
      },
      {
        Action   = "iam:*Provider"
        Effect   = "Deny"
        Resource = "*"
        Sid      = "DenyPermBoundaryBCGovIDPAlteration"
      },
      {
        Action   = "dynamodb:DeleteTable"
        Effect   = "Deny"
        Resource = "arn:aws:dynamodb:*:*:table/BCGOV_IAM_USER_TABLE"
        Sid      = "DenyIAMUserTableDeletion"
      },
      {
        Action   = "elasticloadbalancing:DeleteLoadBalancer"
        Effect   = "Deny"
        Resource = "arn:aws:elasticloadbalancing:ca-central-1:*:loadbalancer/app/default/*"
        Sid      = "DenyPermBoundaryALBAlteration"
      },
      {
        Action = [
          "iam:Create*",
          "iam:Update*",
          "iam:Delete*",
          "iam:DetachRolePolicy",
          "iam:DeleteRolePolicy",
          "iam:AttachRolePolicy",
          "iam:DeleteUserPermissionsBoundary",
          "iam:PutUserPermissionsBoundary",
          "iam:DeleteRolePermissionsBoundary"
        ]
        Effect = "Deny"
        Resource = [
          "arn:aws:iam::*:policy/BCGOV*",
          "arn:aws:iam::*:role/CloudCustodian",
          "arn:aws:iam::*:role/AWSCloudFormationStackSetExecutionRole",
          "arn:aws:iam::*:role/*BCGOV*",
          "arn:aws:iam::*:instance-profile/EC2-Default-SSM-AD-Role-ip",
          "arn:aws:iam::*:user/*"
        ]
        Sid = "DenyPermBoundaryBCGovAlteration"
      },
      {
        Action = [
          "budgets:DeleteBudgetAction",
          "budgets:UpdateBudgetAction",
          "budgets:ModifyBudget"
        ]
        Effect   = "Deny"
        Resource = "arn:aws:budgets::*:budget/Default*"
        Sid      = "DenyDefaultBudgetAlteration"
      },
      {
        Action = [
          "iam:DeleteInstanceProfile",
          "iam:RemoveRoleFromInstanceProfile"
        ]
        Effect   = "Deny"
        Resource = "arn:aws:iam::*:instance-profile/EC2-Default-SSM-AD-Role-ip"
        Sid      = "DenyDefaultInstanceProfileAlteration"
      },
      {
        Action = [
          "kms:ScheduleKeyDeletion",
          "kms:DeleteAlias",
          "kms:DisableKey",
          "kms:UpdateAlias"
        ]
        Effect   = "Deny"
        Resource = "*"
        Condition = {
          "ForAnyValue:StringEquals" = {
            "aws:ResourceTag/Accelerator" = "PBMM"
          }
        }
        Sid = "DenyDefaultKMSAlteration"
      },
      {
        Action = [
          "ssm:DeleteParameter*",
          "ssm:PutParameter"
        ],
        Effect = "Deny",
        "Resource" : [
          "arn:aws:ssm:*:*:parameter/cdk-bootstrap/pbmmaccel/*",
          "arn:aws:ssm:*:*:parameter/octk/*"
        ],
        Sid = "DenyDefaultParameterStoreAlteration"
      },
      {
        Action = [
          "secretsmanager:DeleteSecret",
          "secretsmanager:CreateSecret",
          "secretsmanager:UpdateSecret"
        ]
        Effect   = "Deny"
        Resource = "arn:aws:secretsmanager:*:*:secret:accelerator*"
        Sid      = "DenyDefaultSecretManagerAlteration"
      },
      {
        Action = [
          "aws-marketplace:*"
        ]
        Effect   = "Deny"
        Resource = "*"
        Sid      = "DenyAWSMarketplace"
      }
    ]
  })
}

resource "aws_iam_policy" "bcgov_cost_explorer_and_budgets" {
  name        = "BCGOV_CostExplorerAndBudgets"
  description = "Give all access to Cost Explorer and Budgets"
  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Sid      = "AllowCostExplorer"
        Effect   = "Allow"
        Action   = "ce:*"
        Resource = "*"
      },
      {
        Sid      = "AllowBudgets"
        Effect   = "Allow"
        Action   = "budgets:*"
        Resource = "*"
      }
    ]
  })
}
